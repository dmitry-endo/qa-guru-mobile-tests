plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
    id "io.freefair.lombok" version "8.4"
}

group = 'ru.infosec'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def aspectjWeaverVersion = "1.9.22",
    selenideVersion = "6.13.0",
//    selenideVersion = "7.9.4",
    appiumVersion = "8.3.0",
    restAssuredVersion = "5.5.5",
    allureVersion = "2.21.0",
    junitVersion = "5.9.3",
    slf4jVersion = "2.0.7",
    ownerVersion = "1.0.12",
    commonsVersion = "2.20.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.aspectj:aspectjweaver:$aspectjWeaverVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:$appiumVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "commons-io:commons-io:$commonsVersion",
    )
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8' // Fixes Cyrillic/Unicode chars
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task onboarding_tests(type: Test) {
    useJUnitPlatform {
        includeTags("onboarding_tests")
    }
}

task search_tests(type: Test) {
    useJUnitPlatform {
        includeTags("search_tests")
    }
}
